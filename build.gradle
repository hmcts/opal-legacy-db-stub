plugins {
  id 'application'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.7'
  id 'org.owasp.dependencycheck' version '12.1.6'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'org.sonarqube' version '7.2.2.6593'
  id 'uk.gov.hmcts.java' version '0.12.67'
}
group = 'uk.gov.hmcts.opal'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

// (Temp?) Fix to solve the issue:
// Toolchain from `executable` property does not match toolchain from `javaLauncher` property.
gradle.taskGraph.whenReady { taskGraph ->
  def task = taskGraph.allTasks.find { it.name.endsWith(".main()") }
  if (task instanceof JavaExec) {
    task.executable = task.javaLauncher.get().executablePath.asFile.absolutePath
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '12.3.1'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "7.20.0"

  sourceSets = [
    sourceSets["main"],
    sourceSets["test"],
    sourceSets["functionalTest"],
    sourceSets["integrationTest"],
    sourceSets["smokeTest"]
  ] as Collection<SourceSet>

  reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    html.required = true
  }
}

tasks.named("sonarqube") {
  dependsOn(tasks.named("jacocoTestReport"))
}
tasks.named("check") {
  dependsOn(tasks.named("integration"))
}

def sonarExclusions = [
  '**/uk/gov/hmcts/opal/stub/services/config/**',
  '**/uk/gov/hmcts/opal/stub/services/Application.java',
]

sonarqube {
  properties {
    property "sonar.projectName", "${rootProject.name}"
    property "sonar.projectKey", "uk.gov.hmcts.opal:stub-services"
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.gradle.skipCompile", 'true'
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = 9999999
  suppressionFile = 'config/owasp/suppressions.xml'

  failOnError = false

  skipConfigurations = [
    "checkstyle",
    "compileOnly",
    "pmd",
    "integrationTest",
    "functionalTest",
    "smokeTest",
    "contractTestRuntimeClasspath",
    "contractTestCompileClasspath"
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

ext {
  log4JVersion = "2.25.3"
}

ext['tomcat.version'] = '10.1.42'
ext['snakeyaml.version'] = '2.0'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.15'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'io.rest-assured', name: 'rest-assured'
  implementation (group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '3.0.1')

  compileOnly 'org.projectlombok:lombok:1.18.42'
  annotationProcessor 'org.projectlombok:lombok:1.18.42'

  testImplementation(platform('org.junit:junit-bom:6.0.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

application {
  mainClass.set('uk.gov.hmcts.opal.legacy.stub.Application')
}

bootJar {
  archiveFileName = "opal-legacy-db-stub.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.register('runAllStyleChecks') {
  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
  dependsOn 'checkstyleSmokeTest'
  dependsOn 'checkstyleIntegrationTest'
  dependsOn 'checkstyleFunctionalTest'
  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
  dependsOn 'pmdSmokeTest'
  dependsOn 'pmdIntegrationTest'
  dependsOn 'pmdFunctionalTest'
}
